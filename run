#!/bin/zsh

THIS_DIR=${0:A:h}
APP=$THIS_DIR/verylog
INTERPRETER=${INTERPRETER:-true}

if [[ $# -eq 0 ]]; then
    echo "usage: $0 [parser options] <verilog file>"
    exit 1
fi

FILENAME="${@[-1]}"
shift -p 1

if [[ ! -f $FILENAME ]]; then
    echo "'$FILENAME' is not a regular file"
    exit 1
fi

IVL_CHECK=$(which ivl)

if [[ ! -x "$IVL_CHECK" ]]; then
    echo "No ivl executable found in the PATH"
    exit 1
fi

if [[ -x "$(which rlwrap)" ]]; then
    alias sicstus='rlwrap sicstus'
else
    alias sicstus='sicstus'
fi

IVL_DIR=${IVL_CHECK:A:h}
IVL=$IVL_DIR/ivl
IVLPP=$IVL_DIR/ivlpp/ivlpp

LOGFILE=$THIS_DIR/.log.$(date +%F-%H-%M-%S)

print_IR() {
    (  echo '% vim: set ft=prolog\n:- discontiguous register/1, wire/1.' && $IVL $@ -O >(cat) =($IVLPP $FILENAME) &>$LOGFILE && sed -n --posix 's|^[ \t]*//[ \t]*@annot{\(.*\)}[^}]*|\1.|p' $FILENAME ) || echo 'parse_failed(true).' 
}

IR_FILE=${FILENAME:A:h}/.${FILENAME:t:r:r:r}.pl

print_IR $@ > $IR_FILE
last_err=$?

if [[ $last_err -ne 0 ]]; then
    echo "error while generating the IR file"
    exit 1
fi

if $INTERPRETER; then
    pushd $THIS_DIR

    sicstus \
        -f --noinfo --nologo \
        -l 'src/verilog.pl' \
        --goal '(main -> halt; halt(1)).' \
        -- $IR_FILE
    
    last_err=$?

    if [[ $last_err -ne 0 ]]; then
        echo "[PROLOG ERROR]" 1>&2
        cat $LOGFILE
        exit 1 
    else
        rm -f $LOGFILE
    fi

    popd
else
    if make -s && $APP $IR_FILE
    then
        rm -f $LOGFILE
    else
        echo "[PROLOG ERROR]" 1>&2
        cat $LOGFILE
        exit 1 
    fi
fi
